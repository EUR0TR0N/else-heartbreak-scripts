# Arcade Machine Secret
# For use in the Yvonne Club Arcade Machine
# Has the same game functionality as the original, 
# but boots Charlemagne OS v1.3 with Slurp capability when the "up" key is pressed


var highscore = LoadMemory("highscore")
var champion = LoadMemory("champion")


PlaySound("Powerup 5")
Sleep(0.3)
PlaySound("Powerup 5")
Sleep(0.3)
PlaySound("Powerup 5")
Sleep(0.3)

var score = 0
number id = -1

var ballX = 20
var ballY = 50

var startSpeed = -200

var ballSpeedX = startSpeed
var ballSpeedY = startSpeed

var paddleSpeed = 300

var paddleX = 250
var paddleY = 220

var paddleSize = 50

var lastT = Time()

ClearText()

loop
	number dt = Time() - lastT
	lastT = Time()
	OnInput(dt)
	UpdateBall(dt)
	DrawScore()
	SetRainbowColor()
	DrawBall(ballX, ballY)
	DrawPaddle(paddleX, paddleY)
	DisplayGraphics()
end

void OnInput(number dt)
	if(IsKeyPressed("left"))
		paddleX = paddleX - paddleSpeed * dt
	else if(IsKeyPressed("right"))
		paddleX = paddleX + paddleSpeed * dt
	else if(IsKeyPressed("up"))
		# Charlemagne OS 
		# Slurp-capable
		# English Language
		# By EUR0TR0N
		# For use in computers with internet modem, floppy drive and memory module

		ClearText()
		PlaySound("Coin 1")
		Bar()
		Print("   Charlemagne OS v1.3-X     ")
		Print("  * * * * * * * * * * * *    ")
		Print("     (C) EUR0TR0N 1992       ")
		Print("Released under The GNU GPL v3")
		Bar()
		Sleep(2)
		Print("Booting...")
		Sleep(1)
		Print("OK.")

		loop
			string cmd = Input("root> ")

			if cmd == "info"
				Info()
			else if cmd == "h" or cmd == "help"
				Help()
			else if cmd == "idconnect"
				EstablishIdConnection()
			else if cmd == "ipconnect"
				EstablishIpConnection()
			else if cmd == "store"
				Store()
			else if cmd == "restore"
				Restore()
			else if cmd == "remote"
				RemoteCall()
			else if cmd == "boot"
				BootFromFloppy()
			else if cmd == "load"
				LoadFloppy()
			else if cmd == "portal"
				Slurp()
			else
				Print("Can't understand command " + cmd)
				Print("Enter h for help.")
			end
			Sleep(1)
		end
	end
end

void Help()
	Print("Basic commands:")
	Print(" - h or help")
	Print(" - info")
	Print(" - store")
	Print(" - restore")
	Input("4/9 PRESS ENTER")
	Print("Internet commands:")
	Print(" - idconnect")
	Print(" - ipconnect")
	Print(" - remote")
	Input("7/9 PRESS ENTER")
	Print("Floppy commands:")
	Print(" - load")
	Print(" - boot")
end

void LoadFloppy()
	if HasFloppy()
		Print("")
		Print("Data on floppy:")
		Print("")
		var allData = LoadData()
		loop data in allData
			Print(data)
			Sleep(0.75)
		end
		Print("---END OF FILE")
	else
		Print("Please insert floppy...")
	end
end

void RemoteCall()
	string fnName = Input(id+"> ")
	RemoteFunctionCall(id, fnName, [])
end

void Restore()
	string key = Input("Key: ")
	Print(LoadMemory(key))
end

void Store()
	string key = Input("Key: ")
	string data = Input("Data: ")
	SaveMemory(key, data)
	Print("Data was saved.")
end

void EstablishIpConnection()
	number connection_ip = Input("IP: ")
	Print("Connecting to IP "+connection_ip)
	id = connection_ip
end

void EstablishIdConnection()
	string connectionId = Input("Id: ")
	id = Connect(connectionId)
	if id > -1
		Print("Connection Successful.")
	end
end

void Bar()
	Print("=============================")
end

void DrawScore()
  Color(1, 1, 0)
	Text(1, 1, "Score: " + score)
	Text(1, 2, "Highscore: " + highscore + " by " + champion)
end

void SetRainbowColor()
	number t = Repeat(Time(), 1)
	var col = HSVtoRGB(t, 1, 1)
	Color(col[0], col[1], col[2])
end

void DrawBall(number x, number y)
	var size = 3
	Rect(x - size, y - size, x + size, y + size)
end

void DrawPaddle(number x, number y)
	Line(x, y, x + paddleSize, y)
end

void UpdateBall(number dt)
	ballX = ballX + ballSpeedX * dt
	ballY = ballY + ballSpeedY * dt

	if ballX > 512 and ballSpeedX > 0
		ballX = 512
		ballSpeedX = -ballSpeedX
		PlaySound("Hit 2")
	end
	
	if ballX < 0 and ballSpeedX < 0
		ballX = 0
		ballSpeedX = -ballSpeedX
		PlaySound("Hit 2")
	end

	if WithinPaddleBounds() and ballSpeedY > 0
		# Collide with paddle?
		if ballX > paddleX and ballX < paddleX + paddleSize
			if ballX < paddleX + 15 and ballSpeedX > 0
				# spin
				ballSpeedX *= -1.2
			else if ballX > paddleX + 35 and ballSpeedX < 0
				# spin
				ballSpeedX *= -1.2
			else
				# calm down
				ballSpeedX *= 0.95
			end
			ballY = paddleY
			ballSpeedY = -ballSpeedY
			ballSpeedY *= 1.05
			score++
			PlaySound("Hit 1")
			return
		end
	end

	if ballY > 256 and ballSpeedY > 0
		ballSpeedY = -ballSpeedY
		Print("Ouch!")
		PlaySound("Lose")
		#Print("Score: " + score + ", highscore: " + highscore)
		Sleep(2)
		ClearText()
		if score > highscore
			Highscore()
		end
		score = 0
		ballX = 256
		ballY = 220
		ballSpeedY = startSpeed
		if Random() > 0.5
			ballSpeedX = startSpeed
		else
			ballSpeedX = -startSpeed
		end
	end
	
	if ballY < 0 and ballSpeedY < 0
		ballSpeedY = -ballSpeedY
		PlaySound("Hit 2")
	end
end

bool WithinPaddleBounds()
	return ballY > paddleY - 10 and ballY < paddleY + 5
end

void Highscore()
	Print("You beat the highscore! (" + score + ")")
	champion = Input("Please enter your name: ")
	SaveMemory("champion", champion)
	SaveMemory("highscore", score)
	highscore = score
	Sleep(1)
	ClearText()
	Print("Gratz!")
	Sleep(1)
	ClearText()
end
